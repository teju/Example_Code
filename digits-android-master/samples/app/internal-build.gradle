/*
 * Copyright (C) 2015 Twitter, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.0.4'
        classpath 'com.android.tools.build:gradle:2.1.0'
        classpath 'io.fabric.tools:gradle:1.21.6'
    }
}

// Load API Key information in from property file
Properties props = new Properties()
File propsFile = file('fabric.properties')
if (propsFile.exists()) {
    props.load(propsFile.newDataInputStream())
} else if (System.env.TWITTER_KEY != null && System.env.TWITTER_SECRET != null) {
    props.setProperty("twitterConsumerKey", System.env.TWITTER_KEY)
    props.setProperty("twitterConsumerSecret", System.env.TWITTER_SECRET)
} else {
    throw new GradleException("Missing fabric.properties, check the fabric.properties.sample file.");
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'spoon'


repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "com.example.app.digits"
        versionCode 1
        versionName "1.0"
        minSdkVersion 9
        targetSdkVersion rootProject.targetSdkVersion
        testInstrumentationRunner "com.example.app.SpoonInstrumentationTestRunner"
    }

    buildTypes {
        debug {
            buildConfigField "String", "CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
            buildConfigField "String", "CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
            buildConfigField "String", "PARTNER_KEY", "\"${props.getProperty("twitterPartnerKey")}\""
        }
        release {
            buildConfigField "String", "CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
            buildConfigField "String", "CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
            buildConfigField "String", "PARTNER_KEY", "\"${props.getProperty("twitterPartnerKey")}\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
    }

    packagingOptions {
        exclude 'LICENSE.txt'
    }

    spoon {
        if (project.hasProperty('spoonClassName')) {
            className = project.spoonClassName
        }
    }

    lintOptions {
        warningsAsErrors true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    debugCompile project(path: ':sdk:digits:digits', configuration: 'debug')
    releaseCompile project(path: ':sdk:digits:digits', configuration: 'release')

    compile rootProject.appCompat
    compile rootProject.answers
    debugCompile rootProject.leakCanary
    releaseCompile rootProject.leakCanaryNoOp

    androidTestCompile rootProject.spoon
    androidTestCompile rootProject.espresso
    androidTestCompile rootProject.androidTestingSupport
}

task setupCi << {
    propsFile.write(
            "apiKey=$System.env.FABRIC_KEY\n" +
            "apiSecret=$System.env.FABRIC_SECRET\n"
    )
}
